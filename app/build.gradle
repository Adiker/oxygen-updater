apply plugin: 'com.android.application'
import org.apache.tools.ant.taskdefs.condition.Os


android {
    signingConfigs {
        ubuntuReleaseConfig {
            keyAlias 'androidappskey'
            keyPassword 'Stoomtrein1'
            storeFile file('/home/arjan/keyStore.jks')
            storePassword 'Stoomtrein1'
        }
        macReleaseConfig {
            keyAlias 'androidappskey'
            keyPassword 'Stoomtrein1'
            storeFile file('/Users/arjan.vlek/development/personal/keyStore.jks')
            storePassword 'Stoomtrein1'
        }
    }
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        return true
    }
    defaultConfig {
        applicationId "com.arjanvlek.oxygenupdater"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 4
        versionName "1.2.0"
        if (Os.isFamily(Os.FAMILY_MAC)) {
            signingConfig signingConfigs.macReleaseConfig
        } else {
            signingConfig signingConfigs.ubuntuReleaseConfig
        }
    }
    buildTypes {
        release { // Config for releases, or testing on a real device. Uses the production server, and reads system properties using the OnePlus / OxygenOS specific build.prop values.
            buildConfigField "String", "SERVER_BASE_URL", "\"https://oxygenupdater.com/api/v1.1/\""
            buildConfigField "String", "FAQ_SERVER_URL", "\"https://oxygenupdater.com/inappfaq\"";

            buildConfigField "String", "NOTIFICATIONS_PREFIX", "\"\""
            buildConfigField "String", "DEVICE_NAME_LOOKUP_KEY", "\"ro.display.series\""
            buildConfigField "String", "DEVICE_CODENAME_LOOKUP_KEY", "\"ro.build.product\""
            buildConfigField "String", "OS_VERSION_NUMBER_LOOKUP_KEY", "\"ro.build.ota.versionname\""
            buildConfigField "String", "OS_OTA_VERSION_NUMBER_LOOKUP_KEY", "\"ro.build.version.ota\""
            buildConfigField "String", "BUILD_FINGERPRINT_LOOKUP_KEY", "\"ro.build.oemfingerprint\""
            buildConfigField "String", "SUPPORTED_BUILD_FINGERPRINT_KEYS", "\"release-keys\"" // Only devices using a properly signed (a.k.a. official) version of OxygenOS are supported.
            minifyEnabled true
            shrinkResources false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug { // Config for use during debugging. Uses the test server, and reads system properties using the default build.prop values present in any Android device / emulator.
            buildConfigField "String", "SERVER_BASE_URL", "\"https://test.oxygenupdater.com/api/v1.1/\""
            buildConfigField "String", "FAQ_SERVER_URL", "\"https://test.oxygenupdater.com/inappfaq\"";
            buildConfigField "String", "NOTIFICATIONS_PREFIX", "\"test_\""
            buildConfigField "String", "DEVICE_NAME_LOOKUP_KEY", "\"ro.product.name\""
            buildConfigField "String", "DEVICE_CODENAME_LOOKUP_KEY", "\"ro.product.name\""
            buildConfigField "String", "OS_VERSION_NUMBER_LOOKUP_KEY", "\"ro.build.version.release\""
            buildConfigField "String", "OS_OTA_VERSION_NUMBER_LOOKUP_KEY", "\"ro.build.version.incremental\""
            buildConfigField "String", "BUILD_FINGERPRINT_LOOKUP_KEY", "\"ro.build.fingerprint\""
            buildConfigField "String", "SUPPORTED_BUILD_FINGERPRINT_KEYS", "\"\"" // During debugging, all devices are supported.
            debuggable true
            if (Os.isFamily(Os.FAMILY_MAC)) {
                signingConfig signingConfigs.macReleaseConfig
            } else {
                signingConfig signingConfigs.ubuntuReleaseConfig
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    productFlavors {
    }
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.6.0'
    }
}

repositories {
    mavenCentral()
}



dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //General Android libraries
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.android.support:support-v4:25.1.1'
    //JSON libraries
    compile 'com.fasterxml.jackson.core:jackson-core:2.8.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.0'
    //Date and time libraries
    compile 'joda-time:joda-time:2.9.4'
    compile 'org.joda:joda-convert:1.8.1'
    //Firebase libraries
    compile 'com.google.firebase:firebase-ads:9.2.1'
    compile 'com.google.firebase:firebase-messaging:9.2.1'

    // Java 8 backports:
    compile 'net.sourceforge.streamsupport:streamsupport:1.5.3'
    compile 'net.sourceforge.streamsupport:streamsupport-cfuture:1.5.3'
    compile 'net.sourceforge.streamsupport:streamsupport-atomic:1.5.3'
    compile 'net.sourceforge.streamsupport:streamsupport-flow:1.5.3'
    compile 'net.sourceforge.streamsupport:streamsupport-literal:1.5.3'
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'me.tatarka.retrolambda'

retrolambda {
    javaVersion JavaVersion.VERSION_1_7
    defaultMethods true
}